/PROG  HANOI_SORT_REC
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 970;
CREATE		= DATE 22-12-08  TIME 12:51:50;
MODIFIED	= DATE 22-12-09  TIME 14:55:38;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 44;
MEMORY_SIZE	= 1478;
PROTECT		= READ;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/MN
   1:  R[77:START_TOW]=AR[1]    ;
   2:  R[78:AUX_TOW]=AR[2]    ;
   3:  R[79:DEST_TOW]=AR[3]    ;
   4:  R[73:LARGEST_2FIND]=AR[4]    ;
   5:   ;
   6:  LBL[1] ;
   7:  IF R[73:LARGEST_2FIND]<R[83:SMALLEST],JMP LBL[20] ;
   8:   ;
   9:  R[73:LARGEST_2FIND]=R[73:LARGEST_2FIND]-1    ;
  10:  CALL HANOI_FIND_OBJECT(R[73:LARGEST_2FIND]) ;
  11:   ;
  12:  IF R[76:OBJ_FOUND]=AR[2],JMP LBL[1] ;
  13:   ;
  14:  R[77:START_TOW]=R[76:OBJ_FOUND]    ;
  15:  R[79:DEST_TOW]=AR[2]    ;
  16:  R[78:AUX_TOW]=6-R[77:START_TOW]    ;
  17:  R[78:AUX_TOW]=R[78:AUX_TOW]-R[79:DEST_TOW]    ;
  18:   ;
  19:  CALL HANOI_SORT_REC(R[77:START_TOW],R[78:AUX_TOW],R[79:DEST_TOW],R[73:LARGEST_2FIND]) ;
  20:   ;
  21:  R[77:START_TOW]=AR[1]    ;
  22:  R[78:AUX_TOW]=AR[2]    ;
  23:  R[79:DEST_TOW]=AR[3]    ;
  24:  R[73:LARGEST_2FIND]=AR[4]    ;
  25:   ;
  26:  SELECT R[77:START_TOW]=1,CALL HANOI_PICK_TA ;
  27:         =2,CALL HANOI_PICK_TB ;
  28:         =3,CALL HANOI_PICK_TC ;
  29:  SELECT R[79:DEST_TOW]=1,CALL HANOI_PLACE_TA ;
  30:         =2,CALL HANOI_PLACE_TB ;
  31:         =3,CALL HANOI_PLACE_TC ;
  32:   ;
  33:  R[73:LARGEST_2FIND]=R[73:LARGEST_2FIND]-1    ;
  34:  CALL HANOI_FIND_OBJECT(R[73:LARGEST_2FIND]) ;
  35:   ;
  36:  R[77:START_TOW]=R[76:OBJ_FOUND]    ;
  37:  R[79:DEST_TOW]=AR[3]    ;
  38:  R[78:AUX_TOW]=6-R[77:START_TOW]    ;
  39:  R[78:AUX_TOW]=R[78:AUX_TOW]-R[79:DEST_TOW]    ;
  40:   ;
  41:  CALL HANOI_SORT_REC(R[77:START_TOW],R[78:AUX_TOW],R[79:DEST_TOW],R[73:LARGEST_2FIND]) ;
  42:   ;
  43:  LBL[20] ;
  44:  END ;
/POS
/END

