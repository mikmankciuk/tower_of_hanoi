/PROG  HANOI_SORT_REC_V
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 1232;
CREATE		= DATE 22-12-08  TIME 13:10:44;
MODIFIED	= DATE 22-12-09  TIME 14:55:10;
FILE_NAME	= HANOI_SO;
VERSION		= 0;
LINE_COUNT	= 52;
MEMORY_SIZE	= 1708;
PROTECT		= READ;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL
/MN
   1:  R[77:START_TOW]=AR[1]    ;
   2:  R[78:AUX_TOW]=AR[2]    ;
   3:  R[79:DEST_TOW]=AR[3]    ;
   4:  R[73:LARGEST_2FIND]=AR[4]    ;
   5:   ;
   6:  IF R[97:HANOI_TA_HEIGHT]=0 OR R[98:HANOI_TB_HEIGHT]=0 OR R[99:HANOI_TC_HEIGHT]=0,JMP LBL[20] ;
   7:   ;
   8:  IF R[R[87]]>R[94:HANOI_MODEL_ID] OR R[R[88]]>R[94:HANOI_MODEL_ID] OR R[R[89]]>R[94:HANOI_MODEL_ID],JMP LBL[20] ;
   9:   ;
  10:  LBL[1] ;
  11:  IF R[73:LARGEST_2FIND]<R[83:SMALLEST],JMP LBL[20] ;
  12:   ;
  13:  R[73:LARGEST_2FIND]=R[73:LARGEST_2FIND]-1    ;
  14:  CALL HANOI_FIND_OBJECT(R[73:LARGEST_2FIND]) ;
  15:   ;
  16:  IF R[76:OBJ_FOUND]=AR[2],JMP LBL[1] ;
  17:   ;
  18:  R[77:START_TOW]=R[76:OBJ_FOUND]    ;
  19:  R[79:DEST_TOW]=AR[2]    ;
  20:  R[78:AUX_TOW]=6-R[77:START_TOW]    ;
  21:  R[78:AUX_TOW]=R[78:AUX_TOW]-R[79:DEST_TOW]    ;
  22:   ;
  23:  CALL HANOI_SORT_REC_V(R[77:START_TOW],R[78:AUX_TOW],R[79:DEST_TOW],R[73:LARGEST_2FIND]) ;
  24:   ;
  25:  R[77:START_TOW]=AR[1]    ;
  26:  R[78:AUX_TOW]=AR[2]    ;
  27:  R[79:DEST_TOW]=AR[3]    ;
  28:  R[73:LARGEST_2FIND]=AR[4]    ;
  29:   ;
  30:  IF R[97:HANOI_TA_HEIGHT]=0 OR R[98:HANOI_TB_HEIGHT]=0 OR R[99:HANOI_TC_HEIGHT]=0,JMP LBL[20] ;
  31:   ;
  32:  IF R[R[87]]>R[94:HANOI_MODEL_ID] OR R[R[88]]>R[94:HANOI_MODEL_ID] OR R[R[89]]>R[94:HANOI_MODEL_ID],JMP LBL[20] ;
  33:   ;
  34:  SELECT R[77:START_TOW]=1,CALL HANOI_PICK_TA ;
  35:         =2,CALL HANOI_PICK_TB ;
  36:         =3,CALL HANOI_PICK_TC ;
  37:  SELECT R[79:DEST_TOW]=1,CALL HANOI_PLACE_TA ;
  38:         =2,CALL HANOI_PLACE_TB ;
  39:         =3,CALL HANOI_PLACE_TC ;
  40:   ;
  41:  R[73:LARGEST_2FIND]=R[73:LARGEST_2FIND]-1    ;
  42:  CALL HANOI_FIND_OBJECT(R[73:LARGEST_2FIND]) ;
  43:   ;
  44:  R[77:START_TOW]=R[76:OBJ_FOUND]    ;
  45:  R[79:DEST_TOW]=AR[3]    ;
  46:  R[78:AUX_TOW]=6-R[77:START_TOW]    ;
  47:  R[78:AUX_TOW]=R[78:AUX_TOW]-R[79:DEST_TOW]    ;
  48:   ;
  49:  CALL HANOI_SORT_REC_V(R[77:START_TOW],R[78:AUX_TOW],R[79:DEST_TOW],R[73:LARGEST_2FIND]) ;
  50:   ;
  51:  LBL[20] ;
  52:  END ;
/POS
/END

